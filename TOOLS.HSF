\ ========================================================================
\ T O O L S

Forth HEX CR ." Tools..."

TARGET-COMPILING
: ?          ( adr -- ) @ . ;
: DEPTH      ( -- n )   SP0 SP@ 2+ - 2/  ;
: .S         0 DEPTH 1-  ?DO I PICK  U. -1 +LOOP  ;

: WORDS     ( -- )
             CR
             0         ( word counter on stack)
             LATEST @
             BEGIN
                DUP
                COUNT 7F AND TYPE SPACE
                SWAP 1+ SWAP ( -- cntr addr )
                NFA>LFA @ DUP
             0= KEY? OR UNTIL
             DROP
             CR U. SPACE t." words" ;

0 [IF]
: SPACES  0 ?DO SPACE LOOP ;
: U.R   ( u n -- )  >R  0  <# #S #>  R> OVER - SPACES TYPE ;
: BOUNDS   OVER + SWAP ;

\ BFox DUMP looks like dos debug, but without segment address
\ modified for TI-99 40 column screen

8 CONSTANT: CHUNK  \ no. of bytes to dump for each line

: .##         ( n --)     S>D <# # # #> TYPE ;
: .####       ( n --)     S>D <# # # # # #> TYPE ;
: .ADR        ( ADR --)   .####  T[CHAR] : EMIT ;
: .BYTES      ( ADR N --) BOUNDS DO  SPACE  I C@ .##   LOOP ;

: ASCII?      ( c -- ? ) BL 1-  T[CHAR] ~ WITHIN  ;

: .ASCII      ( adr n --)    \ print ascii values or '.' for non-printable chars
              BOUNDS
              DO
                 I C@ DUP ASCII?          \ check for printable char (from SPACE to ASCII '~')
                 0= IF DROP  T[CHAR] .    \ replace unprintable chars with '.'
                 THEN EMIT
              LOOP ;

: DUMP        ( offset n -- )
               BOUNDS
               DO                         \ 'I' is the address pointer
                  CR @ I  .ADR            \    print the adr
                  I  CHUNK .BYTES SPACE   \ print 8 bytes of memory
                  I  CHUNK .ASCII         \ print 16 ascii format BYTES
                  KEY? IF LEAVE THEN
               CHUNK +LOOP                \ increment the offset address by 16
               CR ;

[THEN]