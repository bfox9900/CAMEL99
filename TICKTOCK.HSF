\ TICKTOCK.HSF  TMS9901 hardware timer interface for Camel 99 Forth

\ credit to: http://www.unige.ch/medecine/nouspikel/ti99/tms9901.htm#Timer

\ timer resolution:  64 clock periods, thus 64*333 = 21.3 microseconds
\ Max Duration    :  ($3FFF) 16383 *64*333 ns = 349.2 milliseconds

CROSS-ASSEMBLING
CODE: TMR!   (  -- )         \ load TMS9901 timer to max value 3FFF  
             W 3FFF LI,      \ load scratch register W with MAXIMUM timer value
             R12 CLR,        \ CRU addr of TMS9901 = 0
             0   SBO,        \ SET bit 0 to 1, Enter timer mode
             R12 INCT,       \ CRU Address of bit 1 = 2 , I'm not kidding
             W 0E LDCR,      \ Load 14 BITs from R1 into timer
             R12  DECT,      \ go back to address 0
             0    SBZ,       \ reset bit 0, Exits clock mode, starts decrementer
             NEXT,           \ 16 bytes
             END-CODE

CODE: TMR@   ( -- n)         \ read the TMS9901 timer
             0 LIMI,
             TOS PUSH,
             R12 CLR,
             0 SBO,          \ SET bit 0 TO 1, ie: Enter timer mode
             TOS 0F STCR,    \ READ TIMER (14 bits plus mode bit) into W
             TOS  1 SRL,     \ Get rid of mode bit
             0 SBZ,          \ SET bit 1 to zero
             2 LIMI,
             NEXT,
             END-CODE

TARGET-COMPILING
\ : uS@   ( -- n)   TMR@  15 / ;  \ read the TMS9901, calc. duration in micro-secs

\ these should be 21.3 uS ticks. maximum reliable value is 3FF0 due to Forth loop speed
: TICKS     ( n -- ) 3FFF SWAP - TMR! BEGIN  DUP TMR@ >  UNTIL DROP ;

\ 333,000uS / 21.3uS = 15633 ticks, rounded down for code latency (HEX 3D11)
\ THIS DID NOT GIVE ACCURATE DELAYS ON CLASSIC99 SIMULATOR

\ : 333MS     ( -- )  30C7 TICKS ;  \ approximation for Classic99

: MS   ( n -- )  015C MIN 02F * TICKS ;  \ max delay= DECIMAL 348 mS



